plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.5' apply false
    id 'io.spring.dependency-management' version '1.1.7'
}

ext {
    springCloudVersion = '2024.0.0'
    awsSdkBom = '2.21.29'
    lombokVersion = '1.18.32'
    jjwtVersion = '0.11.5'
    springdocVersion = '2.8.0'
}

allprojects {
    group = 'org.ddcn41'
    version = '0.0.1-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:3.5.5"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "software.amazon.awssdk:bom:${awsSdkBom}"
        }
        dependencies {
            dependency "org.projectlombok:lombok:${lombokVersion}"
            dependency "io.jsonwebtoken:jjwt-api:${jjwtVersion}"
            dependency "io.jsonwebtoken:jjwt-impl:${jjwtVersion}"
            dependency "io.jsonwebtoken:jjwt-jackson:${jjwtVersion}"
            dependency "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springdocVersion}"
        }
    }

    // 공통 의존성 (모든 서브 모듈)
    dependencies {
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    test {
        useJUnitPlatform()
    }

    // 기본적으로 bootJar 비활성화 (실행 모듈만 활성화)
    tasks.withType(org.springframework.boot.gradle.tasks.bundling.BootJar) {
        enabled = false
    }
    tasks.withType(Jar) {
        enabled = true
    }
}